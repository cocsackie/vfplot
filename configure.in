dnl autoconf configure.in
dnl J.J.Green 2007
dnl $Id: configure.in,v 1.49 2007/12/29 00:01:19 jjg Exp jjg $

AC_INIT(src/vfplot/main.c)

MAJOR_VERSION=0
MINOR_VERSION=9.3
VERSION="$MAJOR_VERSION.$MINOR_VERSION"

AC_PROG_CC

if eval "test $GCC"; then 
   CFLAGS="$CFLAGS -Wall" 
fi

AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_PATH_PROG(GGO,gengetopt)

AC_HEADER_STDC

AC_CHECK_HEADER(sys/time.h)
AC_CHECK_HEADER(sys/resource.h)
AC_CHECK_HEADER(pthread.h)
AC_CHECK_HEADER(netcdf.h)

AC_CHECK_FUNCS(sincos)
AC_CHECK_FUNCS(getrusage)

AC_CHECK_LIB(m,sin)
AC_CHECK_LIB(m,sincos)
AC_CHECK_LIB(netcdf,nc_open)
AC_CHECK_LIB(cunit,CU_add_test)

AC_ARG_ENABLE(kdtree-pthread,
        AC_HELP_STRING([--enable-kdtree-pthread],
			[use pthreads for kdtree list allocation (default is no)]),
	[AC_CHECK_HEADER(pthread.h)
	AC_SUBST(KDTREE_PTHREAD,"-DUSE_LIST_NODE_ALLOCATOR")
	AC_SUBST(PTHREAD,"-pthread")])

AC_ARG_ENABLE(gerris,
        AC_HELP_STRING([--enable-gerris],
			[support reading gerris flow simulation files (default is no)]),
	[CFLAGS="$CFLAGS `pkg-config --cflags gerris2D`"
	CPPFLAGS="$CPPLAGS `pkg-config --cflags gerris2D`"
	AC_CHECK_HEADER(glib.h)
	AC_CHECK_HEADER(gmodule.h)
	AC_CHECK_HEADER(gts.h)
	AC_CHECK_HEADER(gfs.h,
		[AC_DEFINE(HAVE_GFS_H,1,[Define to 1 if you have <gfs.h>])
		AC_SUBST(WITH_GFS,[yes],[no])])
	LDFLAGS="$LDFLAGS -pthread -Wl,--export-dynamic"
	AC_CHECK_LIB(rt,mq_open)
	AC_CHECK_LIB(dl,dlopen)
	AC_CHECK_LIB(glib-2.0,g_allocator_new)
	AC_CHECK_LIB(gmodule-2.0,g_module_open)
	AC_CHECK_LIB(gthread-2.0,g_thread_init)
	AC_CHECK_LIB(gts,gts_object_new)
	AC_CHECK_LIB(gfs2D,gfs_simulation_read)])

AC_ARG_ENABLE(dmalloc,
        AC_HELP_STRING([--enable-dmalloc],[use dmalloc memory checker (default is no)]),
        [AC_DEFINE(USE_DMALLOC,1,[Define to 1 to use the dmalloc library]) 
	AC_CHECK_HEADER(dmalloc.h)
	AC_CHECK_LIB(dmalloc,malloc)])

AC_ARG_ENABLE(profile,
        AC_HELP_STRING([--enable-profile],[instrument for profiling (default is no)]),
        [AC_SUBST(GCC_PROFILING_CFLAGS,["-pg -fno-omit-frame-pointer"])
	AC_PATH_PROG(GPROF,gprof)])

AC_SUBST(INSTALL_PROGRAM)
AC_SUBST(INSTALL_DATA)
AC_SUBST(VERSION)

AC_CONFIG_HEADER(src/include/config.h)

AC_OUTPUT([Makefile src/Common.mk])






